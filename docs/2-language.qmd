---
title: Language
output-file: language
order: 2
code-block-bg: true
code-copy: false
code-line-numbers: false
---

Studyflow diagrams are visual representations of scientific workflows, designed to enhance clarity, rigor, and reproducibility in research processes. They serve as a bridge between human understanding and machine execution. It facilitates better communication among researchers and enabling automated workflows.

## Why?

Trusting science requires rigor and reproducibility, hence clear communication of the experiments has become one of the cornerstones of good scientific practices.

Following the need for clear communication, many scientific projects rely on detailed description of data and protocols in manuscripts, visual diagrams, reporting guidelines, checklists, and more-or-less standard softwares; all to provide enough details to reproduce the findings.

In parallel, as we increasingly rely on machines to facilitate research, trust in code and data are becoming more and more critical. The need for reproducibility of science has become essential for building trust in automated processes, especially at scale using various large models.

The context of studyflow is how scientific workflows and everything related to that (study protocols, experimental designs, data, analysis, etc) can be communicated for rigor and reproducibility in modern days. It is a formal language to represent research processes and data for various stakeholders in a single cohesive visual diagram.


## Grammar


Studyflow diagrams can be described as a domain-specific language to model processes along with data produced or needed by those processes. Studyflow extends the BPMN 2.1 specification (including the addition of the choreography tasks) to fit the needs of specific use cases.

The following EBNF grammar describes the structure of a studyflow diagram:

```ini
Definitions       = { Study } ;

Study             = "Study", identifier, { Attribute }, { Element | SequenceFlow } ;

Element           = Event | Task | Gateway ;

Event             = StartEvent | EndEvent ;

StartEvent        = "StartEvent", identifier, { Attribute } ;
EndEvent          = "EndEvent", identifier, { Attribute } ;

Task              = "Task", identifier, { TaskAttribute }, [ Choreography ] ;
TaskAttribute     = TaskType | Attribute ;
TaskType          = "@type", "CognitiveTest" | "Questionnaire" | "Instruction" | "Rest" | "Script" | "Manual" ;

Choreography      = "choreography", { Attribute }, ParticipantList, [ InitiatingParticipant ] ;

Gateway           = "Gateway", identifier, { GatewayAttribute } ;
GatewayAttribute  = GatewayType | Attribute ;
GatewayType       = "@type", "Random" | "Exclusive" | "Complex" ;

SequenceFlow      = "SequenceFlow", identifier, { Attribute }, NodeRef, "→", NodeRef ;

MessageFlowList   = { "messageFlow", identifier, { Attribute }, ParticipantRef, "→", ParticipantRef } ;

Attribute             = identifier, value ;
ProcessRef            = identifier ;
NodeRef               = identifier ;
ParticipantRef        = ProcessRef ;
InitiatingParticipant = ParticipantRef ;
ParticipantList       = identifier, { identifier } ;

(* low-level components *)
identifier            = letter, { letter | digit | "_" } ;
value                 = string | number | boolean | identifier ;
number                = [ "-" ], digit, { digit }, [ ".", digit, { digit } ] ;
boolean               = "true" | "false" ;
string = '"', { ? Any unicode character except ?
  | "\", (
  '"' (* quotation mark *) |
  "\" (* reverse solidus *) |
  "/" (* solidus *) |
  "b" (* backspace *) |
  "f" (* formfeed *) |
  "n" (* newline *) |
  "r" (* carriage return *) |
  "t" (* horizontal tab *) |
  "u", 4 * ? hex digit ?
  ) }, '"';

letter = [A-Za-z] ;
digit = [0-9] ;
```


An example of a studyflow diagram in this formalism is shown below:

```ini
Study exampleStudy
  StartEvent s

  Task qs
    @type Questionnaire
    language "en"
    text "What is your age?"

  Gateway gw
    @type Random
    condition "ageGroup"

  Task instr
    @type Instruction
    text "Follow carefully"

  Task rest
    @type Rest
    duration 5

  EndEvent e

  SequenceFlow f1 s → qs
  SequenceFlow f2 qs → gw
  SequenceFlow f3 gw → instr
  SequenceFlow f4 gw → e
  SequenceFlow f5 instr → rest
  SequenceFlow f6 rest → e
```

Which can be visualized as a studyflow diagram (colors and layout may vary):

![](assets/img/grammar_example.svg)
